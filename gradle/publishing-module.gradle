apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = rootProject.ext.PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version rootProject.ext.PUBLISH_VERSION

                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar

                pom {
                    name = "LifecycleAwareViewModel"
                    description = 'ViewModel implementation with convenient way to register for lifecycle events'
                    url = 'https://github.com/AdamKobus/lifecycle-aware-viewmodel'
                    licenses {
                        license {
                            name = 'LifecycleAwareViewModel License'
                            url = 'https://github.com/AdamKobus/lifecycle-aware-viewmodel/blob/master/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'AdamKobus'
                            name = 'Adam Kobus'
                            email = 'adam.kobus@appson.org'
                        }
                    }

                    scm {
                        connection = 'scm:git:github.com/AdamKobus/lifecycle-aware-viewmodel.git'
                        developerConnection = 'scm:git:ssh://github.com/AdamKobus/lifecycle-aware-viewmodel.git'
                        url = 'https://github.com/AdamKobus/lifecycle-aware-viewmodel/tree/master'
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications
}
